import com.android.ide.common.signing.KeytoolException

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
File debugKeyStoreFile, releaseKeyStoreFile
if (keystorePropertiesFile.canRead()) {
    // Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    if (keystoreProperties != null) {
        debugKeyStoreFile = file(keystoreProperties['debugStoreFile'])
        releaseKeyStoreFile = file(keystoreProperties['releaseStoreFile'])
    }
} else {
    println '\'keystore.properties\' not found!'
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.tony.qrcodeecommerce"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        debug {
            if (keystoreProperties != null) {
                try {
                    if (debugKeyStoreFile != null && debugKeyStoreFile.canRead()) {
                        keyAlias keystoreProperties['debugKeyAlias']
                        keyPassword keystoreProperties['debugKeyPassword']
                        storeFile debugKeyStoreFile
                        storePassword keystoreProperties['debugStorePassword']
                    }
                } catch (KeytoolException e) {
                    println '\'debug keystore\' not found! ' + e
                }
            }
        }
        release {
            if (keystoreProperties != null) {
                try {
                    if (releaseKeyStoreFile != null && releaseKeyStoreFile.canRead()) {
                        keyAlias keystoreProperties['releaseKeyAlias']
                        keyPassword keystoreProperties['releaseKeyPassword']
                        storeFile releaseKeyStoreFile
                        storePassword keystoreProperties['releaseStorePassword']
                    }
                } catch (KeytoolException e) {
                    println '\'release keystore\' not found! ' + e
                }
            }
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [appName: "第一科大網路購物APP [debug]"]
            applicationIdSuffix '.debug'
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            manifestPlaceholders = [appName: "第一科大網路購物APP"]
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core:1.0.2'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.fragment:fragment:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha08'
    implementation 'com.journeyapps:zxing-android-embedded:3.0.1@aar'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.google.firebase:firebase-core:17.0.0'
    implementation 'se.emilsjolander:stickylistheaders:2.7.0'
    implementation 'com.mcxiaoke.volley:library:1.0.19'
}

apply plugin: 'com.google.gms.google-services'
